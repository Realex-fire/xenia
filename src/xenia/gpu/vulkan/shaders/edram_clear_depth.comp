// NOTE: This file is compiled and embedded into the exe.
//       Use `xb genspirv` and check in any changes under bin/.

#version 450 core
#extension all : warn
#extension GL_ARB_compute_shader : require
#extension GL_ARB_separate_shader_objects : require
#extension GL_ARB_shader_image_load_store : require
#extension GL_ARB_shading_language_420pack : require

// 2 groups per tile (80x16 threads per group is too much).
layout(local_size_x = 40, local_size_y = 16, local_size_z = 1) in;

layout(push_constant) uniform push_consts_type {
  uint offset_tiles;
  uint pitch_tiles;
  uint stencil_depth;
  uint depth_host;
} push_constants;

layout(set = 0, binding = 0, r32ui) uniform writeonly uimage2DArray edram;

void main() {
  uint tile_index = push_constants.offset_tiles + gl_WorkGroupID.y *
                    push_constants.pitch_tiles + (gl_WorkGroupID.x >> 1);
  uint subgroup = gl_WorkGroupID.x & 1;
  uvec3 coord = uvec3(tile_index & 15, tile_index >> 4, 0) * uvec3(80, 16, 0) +
                gl_LocalInvocationID;
  coord.x += subgroup * 40;
  imageStore(edram, ivec3(coord), uvec4(push_constants.stencil_depth));
  coord.z = 1;
  imageStore(edram, ivec3(coord), uvec4(push_constants.depth_host));
}
