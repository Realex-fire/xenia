// NOTE: This file is compiled and embedded into the exe.
//       Use `xb genspirv` and check in any changes under bin/.

#version 450 core
#extension all : warn
#extension GL_ARB_compute_shader : require
#extension GL_ARB_separate_shader_objects : require
#extension GL_ARB_shader_image_load_store : require
#extension GL_ARB_shading_language_420pack : require

// Full tile, but one RT pixel takes 2 EDRAM texels.
layout(local_size_x = 40, local_size_y = 16, local_size_z = 1) in;

layout(push_constant) uniform push_consts_type {
  // In tiles.
  uint edram_offset;
  uint edram_pitch;
  // In render target texels (viewport offset).
  uvec2 rt_offset;
} push_constants;

// 1280x2048 (16x128 tiles).
layout(set = 0, binding = 0, r32ui) uniform writeonly uimage2D edram;
layout(set = 0, binding = 1, rg32ui) uniform readonly uimage2D rt;

void main() {
  uvec4 pixel = imageLoad(rt, ivec2(gl_GlobalInvocationID.xy +
                                    push_constants.rt_offset));
  uint tile_index = push_constants.edram_offset + gl_WorkGroupID.y *
                    push_constants.edram_pitch + gl_WorkGroupID.x;
  uvec2 edram_coord = uvec2(tile_index & 15, tile_index >> 4) * uvec2(80, 16);
  edram_coord.x += gl_LocalInvocationID.x << 1;
  edram_coord.y += gl_LocalInvocationID.y;
  imageStore(edram, ivec2(edram_coord), uvec4(pixel.r));
  ++edram_coord.x;
  imageStore(edram, ivec2(edram_coord), uvec4(pixel.g));
}
