// NOTE: This file is compiled and embedded into the exe.
//       Use `xb genspirv` and check in any changes under bin/.

#version 450 core
#extension all : warn
#extension GL_ARB_compute_shader : require
#extension GL_ARB_separate_shader_objects : require
#extension GL_ARB_shader_image_load_store : require
#extension GL_ARB_shading_language_420pack : require

// 2 groups per tile (80x16 threads per group is too much).
layout(local_size_x = 40, local_size_y = 16, local_size_z = 1) in;

layout(push_constant) uniform push_consts_type {
  uint edram_offset_tiles;
  uint edram_pitch_tiles;
  uint buffer_pitch_px;
} push_constants;

layout(set = 0, binding = 0, r32ui) uniform readonly uimage2DArray edram;
layout(set = 0, binding = 1, r32ui) uniform writeonly uimageBuffer buffer_depth;
layout(set = 0, binding = 2, r8ui)
    uniform writeonly uimageBuffer buffer_stencil;

// Based on the 6e4 code from:
// https://github.com/Microsoft/DirectXTex/blob/master/DirectXTex/DirectXTexConvert.cpp
uint Float20e4To32(uint bits24) {
  uint mantissa = bits24 & 0xFFFFFu;
  uint exponent = (bits24 >> 20u) & 0xFu;
  if (exponent == 0u) {
    if (mantissa != 0u) {
      // Normalize the value in the resulting float.
      // do { Exponent--; Mantissa <<= 1; } while ((Mantissa & 0x100000) == 0);
      uint mantissa_lzcnt = uint(20 - findMSB(mantissa));
      exponent = 1u - mantissa_lzcnt;
      mantissa = (mantissa << mantissa_lzcnt) & 0xFFFFFu;
    } else {
      // Zero - set biased exponent to -120.
      exponent = 0xFFFFFEE0u;
    }
  }
  return ((exponent + 120u) << 23u) | (mantissa << 3u);
}

void main() {
  uint tile_index = push_constants.edram_offset_tiles + gl_WorkGroupID.y *
                    push_constants.edram_pitch_tiles + (gl_WorkGroupID.x >> 1);
  uvec3 edram_coord = uvec3(tile_index & 15, tile_index >> 4, 0) *
                      uvec3(80, 16, 0) + gl_LocalInvocationID;
  edram_coord.x += (gl_WorkGroupID.x & 1) * 40;
  uint stencil_depth = imageLoad(edram, ivec3(edram_coord)).r;
  int pixel_index =
      int(gl_GlobalInvocationID.y * push_constants.buffer_pitch_px +
          gl_GlobalInvocationID.x);
  imageStore(buffer_depth, pixel_index,
             uvec4(Float20e4To32(stencil_depth >> 8u)));
  imageStore(buffer_stencil, pixel_index, uvec4(stencil_depth & 0xFFu));
}
